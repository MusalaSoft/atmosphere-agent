// This file is part of the ATMOSPHERE mobile testing framework.
// Copyright (C) 2016 MusalaSoft
//
// ATMOSPHERE is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// ATMOSPHERE is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with ATMOSPHERE.  If not, see <http://www.gnu.org/licenses/>.

plugins {
    id "java"
    id "maven-publish"
    id "application"
    id "com.jfrog.bintray" version "1.8.1"
}

// bintray publishing parameters
version = '0.1.1'
def organizationName = 'musala'
def repoName = 'atmosphere'
def projectName = 'atmosphere-agent'
def repoUrl = 'https://github.com/MusalaSoft/atmosphere-agent.git'
def description = 'Update the logcat buffer, remove the deprecated dependencies, update the config file, add license notices.'

mainClassName = 'com.musala.atmosphere.agent.Agent'

jar {
    manifest {
        attributes 'Implementation-Title': 'Atmosphere Agent',
                   'Implementation-Version': version,
                   'Main-Class': mainClassName
    }
}

run {
    standardInput = System.in
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven {
        url 'http://repo.typesafe.com/typesafe/maven-releases/'
    }
}

configurations {
    onDeviceComponents
}

dependencies {
    compile 'com.musala.atmosphere:atmosphere-server-agent-lib:0.+'
    compile 'com.musala.atmosphere:atmosphere-agent-device-lib:0.+'
    compile 'commons-logging:commons-logging:1.2'
    compile 'commons-cli:commons-cli:1.2'
    compile 'commons-io:commons-io:2.4'
    compile 'commons-codec:commons-codec:1.8'
    compile 'com.android.tools.ddms:ddmlib:25.1.3'
    compile 'org.hyperic:sigar:1.6.4'
    compile 'com.googlecode.mp4parser:isoparser:1.1.21'
    compile fileTree(dir: 'natives/', include: '*.*')
    compile 'gov.nist.math:scimark:2.0'
    // Fix for: 'Failed to load class "org.slf4j.impl.StaticLoggerBinder"' error message.
    // See http://www.slf4j.org/codes.html#StaticLoggerBinder for further details.
    compile 'org.slf4j:slf4j-simple:1.7.21'
    compile 'io.github.bonigarcia:webdrivermanager:1.5.0'
    compile 'org.seleniumhq.selenium:selenium-java:2.47.1'
    compile 'commons-net:commons-net:3.5'
    compile 'org.reflections:reflections:0.9.9-RC1'

    compile 'org.glassfish.tyrus:tyrus-client:1.13.1'
    compile 'org.glassfish.tyrus:tyrus-container-grizzly-client:1.13.1'

    runtime 'org.apache.httpcomponents:httpclient:4.4.1'
    runtime 'org.apache.httpcomponents:httpcore:4.4.4'

    onDeviceComponents 'com.musala.atmosphere:atmosphere-ime:0.+'
    onDeviceComponents 'com.musala.atmosphere:atmosphere-service:0.+'
    onDeviceComponents 'com.musala.atmosphere:atmosphere-uiautomator-bridge:0.+'

    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'junit:junit:4.12'
}

task copyOnDeviceComponents(type: Copy) {
    into 'onDeviceComponents'
    from configurations.onDeviceComponents
    // exclude version
    rename { fileName -> fileName.replaceAll(/-\d+(\.\d+)+/, '') }
}

build.dependsOn copyOnDeviceComponents

task testJar(type: Jar)

publishing {
    publications {
        atmosphereAgent(MavenPublication) {
            groupId 'com.musala.atmosphere'
            artifactId "$projectName"
            version version

            from components.java

            if (System.getProperty('bintray.user') != null) {
                artifact sourcesJar
                artifact javadocJar
            }

            // Credit: http://stackoverflow.com/a/32353697/3370842
            pom.withXml {
                // Generate map of resolved versions
                Map resolvedVersionMap = [:]
                Set<ResolvedArtifact> resolvedArtifacts = configurations.compile.getResolvedConfiguration().getResolvedArtifacts()
                resolvedArtifacts.addAll(configurations.testCompile.getResolvedConfiguration().getResolvedArtifacts())
                resolvedArtifacts.each {
                    ModuleVersionIdentifier mvi = it.getModuleVersion().getId();
                    resolvedVersionMap.put("${mvi.getGroup()}:${mvi.getName()}", mvi.getVersion())
                }

                // Update dependencies with resolved versions
                asNode().dependencies.first().each {
                    def groupId = it.get("groupId").first().value().first()
                    def artifactId = it.get("artifactId").first().value().first()
                    it.get("version").first().value = resolvedVersionMap.get("${groupId}:${artifactId}")
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }
}

tasks.withType(Test) {
    scanForTestClasses = false
    include "**/*Test.class"
}

// Sigar native libraries
run {
    systemProperty 'java.library.path', file('natives')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

test {
    forkEvery = 1
    systemProperty 'java.library.path', file('natives')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

/*
 This is publishing task that uploads the artifacts to bintray.com.
 see: https://github.com/bintray/gradle-bintray-plugin
 see: https://reflectoring.io/guide-publishing-to-bintray-with-gradle/

 Use the following command for automatic upload to bintray:
 $ ./gradlew bintrayUpload -Dbintray.user=<BINTRAY_USERNAME> -Dbintray.key=<BINTRAY_API_KEY> -Dgpg.pass=<ATMOSPHERE_PASSPHRASE>
*/
bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['atmosphereAgent']

	pkg {
        repo = "$repoName" // existing repository in bintray to add the artifacts to
        name = "$projectName" // package name, current project name
        userOrg = "$organizationName" // the name of the organization, If not added will use 'BINTRAY_USER' by default
        licenses = ['GPL-3.0']
        vcsUrl = "$repoUrl" // your VCS URL
        version {
            name = project.version.toString()
            desc = "$description"
            released  = new Date()

            gpg {
                sign = true // Determines whether to GPG sign the files. The default is false
                passphrase = System.getProperty('gpg.pass') // Optional. The passphrase for GPG signing'
            }
        }
    }
}
